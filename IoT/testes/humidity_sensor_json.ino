// Autor: Vitor Augusto Tib√©rio - Estudante de Engenharia El√©trica - USP S√£o Carlos

#include <WiFi.h>
#include <WebServer.h>
#include <DHT.h>

// Configura√ß√£o do Wi-fi // 
const char* ssid = "XXXXXXX";      
const char* password = "XXXXXXX"; 

// Configurando o DHT11 
#define DHTPIN 4       // Pino conectado ao DATA do sensor
#define DHTTYPE DHT11  // Modelo do sensor

DHT dht(DHTPIN, DHTTYPE);
WebServer server(80);

// Vari√°veis globais para armazenar os valores
float temperatura = 0.0;
float umidade = 0.0;

// Fun√ß√£o para leitura do sensor DHT11 // 
void readSensor() {
  float t = dht.readTemperature();
  float h = dht.readHumidity();

  // S√≥ atualiza as vari√°veis globais se a leitura for v√°lida
  if (!isnan(t) && !isnan(h)) {
    temperatura = t;
    umidade = h;
  } else {
    Serial.println("Falha na leitura do sensor!");
  }
}

// Cria√ß√£o da rota HTML // 
void handleRoot() {
  readSensor(); 
  
  String html = "<!DOCTYPE html><html>";
  html += "<head><meta charset='UTF-8'><meta http-equiv='refresh' content='2'></head>";
  html += "<body style='font-family: sans-serif; text-align: center; margin-top: 50px;'>";
  html += "<h1>Monitoramento ESP32</h1>";
  html += "<p>üå°Ô∏è Temperatura: <strong>" + String(temperatura) + " &deg;C</strong></p>";
  html += "<p>üíß Umidade: <strong>" + String(umidade) + " %</strong></p>";
  html += "<p><small>Para dados brutos acesse: <a href='/dados'>/dados</a></small></p>";
  html += "</body></html>";
  
  server.send(200, "text/html", html);
}

// Cria√ß√£o da rota JSON (para plot do gr√°fico em python) 
void handleData() {
  readSensor();
  
  // Monta o JSON manualmente: {"t": 25.5, "h": 60.0}
  String json = "{\"t\":";
  json += String(temperatura);
  json += ",\"h\":";
  json += String(umidade);
  json += "}";
  
  // Envia com o tipo "application/json"
  server.send(200, "application/json", json);
}

void setup() {
  Serial.begin(115200);
  dht.begin();

  // Conex√£o Wi-Fi
  WiFi.begin(ssid, password);
  Serial.print("Conectando ao WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  
  Serial.println("");
  Serial.print("Conectado! IP: ");
  Serial.println(WiFi.localIP());

  // Define as rotas
  server.on("/", handleRoot);      // Rota padr√£o
  server.on("/dados", handleData); // Rota de dados para o Python

  server.begin();
}

void loop() {
  server.handleClient(); // Mant√©m o servidor atendendo requisi√ß√µes
}
